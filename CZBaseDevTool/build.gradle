apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'
group="com.github.XinYiWorld"       //Github用户名

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    dataBinding{
        enabled true
    }


    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //单元测试
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:multidex:1.0.0'
    //Retrofit+RxJava
    compile 'io.reactivex:rxjava:1.1.3'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'org.ligboy.retrofit2:converter-fastjson-android:2.0.2'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.okio:okio:1.6.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.alibaba:fastjson:1.1.54.android'
    //load picture
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //list-view
    compile 'com.camnter.easyrecyclerview:easyrecyclerview:1.3'
    //MaterialDesignDialog，记得在Project的gradle要配置个maven地址
    compile('com.github.afollestad.material-dialogs:commons:0.8.5.6@aar') {
        transitive = true
    }

    //状态栏变色
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'

}


// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}